-- |
-- Module      : Math.Category.SymbolicNumFunction
-- Copyright   : (c) Justus SagemÃ¼ller 2020
-- License     : GPL v3
-- 
-- Maintainer  : (@) jsag $ hvl.no
-- Stability   : experimental
-- Portability : portable
-- 
{-# LANGUAGE GADTs                 #-}
{-# LANGUAGE TypeFamilies          #-}
{-# LANGUAGE RankNTypes            #-}
{-# LANGUAGE FlexibleInstances     #-}
{-# LANGUAGE MultiParamTypeClasses #-}
{-# LANGUAGE FlexibleContexts      #-}
{-# LANGUAGE UndecidableInstances  #-}
{-# LANGUAGE TypeOperators         #-}
{-# LANGUAGE TypeInType            #-}
{-# LANGUAGE TypeApplications      #-}
{-# LANGUAGE ScopedTypeVariables   #-}
{-# LANGUAGE ConstraintKinds       #-}
{-# LANGUAGE StandaloneDeriving    #-}
{-# LANGUAGE InstanceSigs          #-}
{-# LANGUAGE UnicodeSyntax         #-}

module Math.Category.SymbolicNumFunction where

import qualified Prelude as Hask

import Control.Category.Constrained.Prelude
import Control.Arrow.Constrained

import Data.AdditiveGroup
import Data.VectorSpace

import Data.Typeable (Typeable, eqT)
import Data.Type.Equality ((:~:)(Refl))
import Data.Kind (Type)
import GHC.Exts (Constraint)


data SymbNumFn :: (Type -> Constraint) -> Type -> Type -> Type where
  SymbConst :: (Î¶ c, Typeable c, Eq c) => c -> SymbNumFn Î¶ a c
  SymbId :: SymbNumFn Î¶ a a

  SymbCompo :: SymbNumFn Î¶ b c -> SymbNumFn Î¶ a b -> SymbNumFn Î¶ a c
  SymbPar :: SymbNumFn Î¶ a b -> SymbNumFn Î¶ Î± Î² -> SymbNumFn Î¶ (a,Î±) (b,Î²)
  SymbCopy :: SymbNumFn Î¶ a (a,a)
  SymbSwap :: SymbNumFn Î¶ (a,b) (b,a)
  SymbRegroup :: SymbNumFn Î¶ (a,(b,c)) ((a,b),c)
  SymbRegroup' :: SymbNumFn Î¶ ((a,b),c) (a,(b,c))
  SymbFst :: SymbNumFn Î¶ (a,b) a
  SymbSnd :: SymbNumFn Î¶ (a,b) b

  SymbUnaryElementary :: SymbNumUnaryElementaryFn a -> SymbNumFn Î¶ a a
  SymbBinaryElementary :: SymbNumBinaryElementaryFn a -> SymbNumFn Î¶ (a,a) a
  SymbScalarMul :: VectorSpace v => SymbNumFn Î¶ (v, Scalar v) v
  SymbScalarDiv :: (VectorSpace v, Fractional (Scalar v))
                  => SymbNumFn Î¶ (v, Scalar v) v
  SymbInnerProd :: (InnerSpace v, Î¶ v) => SymbNumFn Î¶ (v, v) (Scalar v)

deriving instance Show (SymbNumFn Show a b)

data SymbNumUnaryElementaryFn a where
  SymbNegate :: AdditiveGroup a => SymbNumUnaryElementaryFn a
  SymbAbs, SymbRelu, SymbSignum :: Num a => SymbNumUnaryElementaryFn a
  SymbRecip :: Fractional a => SymbNumUnaryElementaryFn a
  SymbElementaryFloating
     :: Floating a => SymbElementaryFlFn a -> SymbNumUnaryElementaryFn a

deriving instance Eq (SymbNumUnaryElementaryFn a)
deriving instance Show (SymbNumUnaryElementaryFn a)


data SymbElementaryFlFn :: Type -> Type where
  SymbExp, SymbLog, SymbSqrt, SymbSin, SymbCos, SymbTan, SymbAsin, SymbAcos
   , SymbAtan, SymbSinh, SymbCosh, SymbTanh, SymbAsinh, SymbAcosh, SymbAtanh
      :: SymbElementaryFlFn a

deriving instance Eq (SymbElementaryFlFn a)
deriving instance Show (SymbElementaryFlFn a)

data SymbNumBinaryElementaryFn a where
  SymbAdd, SymbSubtract :: AdditiveGroup a => SymbNumBinaryElementaryFn a
  SymbPow :: Floating a => SymbNumBinaryElementaryFn a
  SymbLogBase :: Floating a => SymbNumBinaryElementaryFn a
  SymbMul :: Num a => SymbNumBinaryElementaryFn a
  SymbDiv :: Fractional a => SymbNumBinaryElementaryFn a

deriving instance Eq (SymbNumBinaryElementaryFn a)
deriving instance Show (SymbNumBinaryElementaryFn a)


instance Category (SymbNumFn Î¶) where
  id = SymbId
  SymbId . g = g
  f . SymbId = f
  SymbConst c . _ = SymbConst c
  f . SymbCompo g Î³ = SymbCompo (f . g) Î³ -- Store composition in left-associative
                                          -- form, to aid common-subexpression
                                          -- elimination in &&&.
  f . g = SymbCompo f g

instance Î¶ () => Cartesian (SymbNumFn Î¶) where
  swap = SymbSwap
  attachUnit = id &&& terminal
  detachUnit = fst
  regroup = SymbRegroup
  regroup' = SymbRegroup'

instance Î¶ () => Morphism (SymbNumFn Î¶) where
  f***g = SymbPar f g

infix 4 ===
(===) :: âˆ€ a b . (Eq a, Typeable a, Typeable b)
   => a -> b -> Maybe (a:~:b)
x===y = case eqT @a @b of
  Just Refl | x==y  -> Just Refl
  _ -> Nothing

instance Î¶ () => PreArrow (SymbNumFn Î¶) where
  SymbId &&& SymbId = SymbCopy
  SymbConst x &&& SymbConst y
   | Just Refl <- x===y
       = SymbCompo SymbCopy (SymbConst x)
  SymbUnaryElementary f &&& SymbUnaryElementary g
   | f==g    = SymbCompo SymbCopy (SymbUnaryElementary f)
  SymbBinaryElementary f &&& SymbBinaryElementary g
   | f==g    = SymbCompo SymbCopy (SymbBinaryElementary f)
  f &&& SymbCompo g Î³
   | SymbCompo SymbCopy h <- f&&&Î³
       = (id&&&g) . h
  SymbCompo f Ï† &&& g
   | SymbCompo SymbCopy h <- Ï†&&&g
       = (f&&&id) . h
  SymbCompo f Ï† &&& SymbCompo g Î³ = case Ï†&&&Î³ of
    SymbCopy -> f&&&g
    SymbCompo SymbCopy h -> (f&&&g) . h
    Ï†Î³ -> (f***g) . Ï†Î³
  SymbPar f Ï† &&& SymbPar g Î³
   | (SymbCompo SymbCopy fg, SymbCompo SymbCopy Ï†Î³) <- (f&&&g, Ï†&&&Î³)
       = SymbCompo SymbCopy (SymbPar fg Ï†Î³)
  SymbCopy &&& SymbCopy = SymbCompo SymbCopy SymbCopy
  SymbSwap &&& SymbSwap = SymbCompo SymbCopy SymbSwap
  SymbRegroup &&& SymbRegroup = SymbCompo SymbCopy SymbRegroup
  SymbRegroup' &&& SymbRegroup' = SymbCompo SymbCopy SymbRegroup'
  SymbFst &&& SymbFst = SymbCompo SymbCopy SymbFst
  SymbSnd &&& SymbSnd = SymbCompo SymbCopy SymbSnd
  SymbScalarMul &&& SymbScalarMul = SymbCompo SymbCopy SymbScalarMul
  SymbInnerProd &&& SymbInnerProd = SymbCompo SymbCopy SymbInnerProd
  SymbScalarDiv &&& SymbScalarDiv = SymbCompo SymbCopy SymbScalarDiv
  f&&&g = SymbCompo (SymbPar f g) SymbCopy

  terminal = SymbConst ()
  fst = SymbFst
  snd = SymbSnd

type CET Î¶ o = (Î¶ o, Eq o, Typeable o)

instance Î¶ () => WellPointed (SymbNumFn Î¶) where
  type PointObject (SymbNumFn Î¶) o = (CET Î¶ o)
  const = SymbConst
  unit = pure ()

type SymbNumVal Î¶ = GenericAgent (SymbNumFn Î¶)

instance HasAgent (SymbNumFn Î¶) where
  type AgentVal (SymbNumFn Î¶) a v = SymbNumVal Î¶ a v
  alg = genericAlg
  ($~) = genericAgentMap

instance Î¶ () => CartesianAgent (SymbNumFn Î¶) where
  alg1to2 = genericAlg1to2
  alg2to1 = genericAlg2to1
  alg2to2 = genericAlg2to2

instance (Î¶ (), CET Î¶ x) => PointAgent (SymbNumVal Î¶) (SymbNumFn Î¶) a x where
  point = genericPoint


instance (AdditiveGroup x, Î¶ (), CET Î¶ x) => AdditiveGroup (SymbNumVal Î¶ a x) where
  zeroV = point zeroV
  (^+^) = genericAgentCombine (SymbBinaryElementary SymbAdd)
  (^-^) = genericAgentCombine (SymbBinaryElementary SymbSubtract)
  negateV = genericAgentMap (SymbUnaryElementary SymbNegate)

instance (VectorSpace v, Î¶ (), CET Î¶ v) => VectorSpace (SymbNumVal Î¶ a v) where
  type Scalar (SymbNumVal Î¶ a v) = SymbNumVal Î¶ a (Scalar v)
  (*^) = flip $ genericAgentCombine SymbScalarMul

instance (VectorSpace n, Num n, n ~ Scalar n, Î¶ (), CET Î¶ n)
            => Num (SymbNumVal Î¶ a n) where
  fromInteger = point . fromInteger
  (+) = (^+^)
  (-) = (^-^)
  (*) = genericAgentCombine (SymbBinaryElementary SymbMul)
  negate = negateV
  abs = genericAgentMap (SymbUnaryElementary SymbAbs)
  signum = genericAgentMap (SymbUnaryElementary SymbSignum)

instance (VectorSpace n, Fractional n, n ~ Scalar n, Î¶ (), CET Î¶ n)
            => Fractional (SymbNumVal Î¶ a n) where
  fromRational = point . fromRational
  recip = genericAgentMap (SymbUnaryElementary SymbRecip)
  (/) = genericAgentCombine (SymbBinaryElementary SymbDiv)

floatingAgentMap :: (VectorSpace n, Floating n, n ~ Scalar n, Î¶ (), CET Î¶ n)
     => SymbElementaryFlFn n -> SymbNumVal Î¶ a n -> SymbNumVal Î¶ a n
floatingAgentMap = genericAgentMap . SymbUnaryElementary . SymbElementaryFloating

instance (VectorSpace n, Floating n, n ~ Scalar n, Î¶ (), CET Î¶ n)
            => Floating (SymbNumVal Î¶ a n) where
  pi = point pi
  logBase = genericAgentCombine (SymbBinaryElementary SymbLogBase)
  exp = floatingAgentMap SymbExp
  log = floatingAgentMap SymbLog
  sqrt = floatingAgentMap SymbSqrt
  sin = floatingAgentMap SymbSin
  cos = floatingAgentMap SymbCos
  tan = floatingAgentMap SymbTan
  sinh = floatingAgentMap SymbSinh
  cosh = floatingAgentMap SymbCosh
  tanh = floatingAgentMap SymbTanh
  asin = floatingAgentMap SymbAsin
  acos = floatingAgentMap SymbAcos
  atan = floatingAgentMap SymbAtan
  asinh = floatingAgentMap SymbAsinh
  acosh = floatingAgentMap SymbAcosh
  atanh = floatingAgentMap SymbAtanh


instance EnhancedCat (->) (SymbNumFn Î¶) where
  arr (SymbConst c) _ = c
  arr SymbId x = x

  arr (SymbCompo f g) x = (arr f . arr g) x
  arr (SymbPar f g) (x,y) = (f$x, g$y)
  arr SymbCopy x = (x,x)
  arr SymbSwap (x,y) = (y,x)
  arr SymbRegroup (x,(y,z)) = ((x,y),z)
  arr SymbRegroup' ((x,y),z) = (x,(y,z))
  arr SymbFst (x,_) = x
  arr SymbSnd (_,y) = y

  arr SymbScalarMul (v,Î¼) = Î¼*^v
  arr SymbScalarDiv (v,Î¼) = v^/Î¼
  arr SymbInnerProd (v,w) = v<.>w

  arr (SymbUnaryElementary f) x = case f of
    SymbAbs -> abs x
    SymbNegate -> negateV x
    SymbRelu -> 2*abs x - x
    SymbSignum -> signum x
    SymbRecip -> recip x
    SymbElementaryFloating ð‘“ -> case ð‘“ of
       SymbExp -> exp x
       SymbLog -> log x
       SymbSqrt -> sqrt x
       SymbSin -> sin x
       SymbCos -> cos x
       SymbTan -> tan x
       SymbAsin -> asin x
       SymbAcos -> acos x
       SymbAtan -> atan x
       SymbSinh -> sinh x
       SymbCosh -> cosh x
       SymbTanh -> tanh x
       SymbAsinh -> asinh x
       SymbAcosh -> acosh x
       SymbAtanh -> atanh x
      
  arr (SymbBinaryElementary f) (x,y) = case f of
     SymbAdd -> x^+^y
     SymbMul -> x*y
     SymbDiv -> x/y
     SymbSubtract -> x^-^y
     SymbLogBase -> logBase x y
     SymbPow -> x**y

